{
  config,
  lib,
  ...
}:
let
  cfg = config.konrad.programs.nvim;
in
{
  options.konrad.programs.nvim = {
    enable = lib.mkEnableOption "enable managed nvim";

    package = lib.mkOption {
      type = lib.types.package;
      description = "Package";
      example = "pkgs.nvim";
    };

    extendGitIgnores = lib.mkOption {
      type = lib.types.bool;
      default = true;
      description = ''
        Whether to add misc files generated by neovim, lsp, dap etc. to default global git ignore.
      '';
    };

    notesPath = lib.mkOption {
      type = lib.types.nullOr lib.types.path;
      default = null;
      description = ''
        Absolute path to root folder for quick-notes functionality.
        Provide null to keep this in XDG state folder.
      '';
    };

    spellPath = lib.mkOption {
      type = lib.types.nullOr lib.types.path;
      default = null;
      description = ''
        Absolute path to root folder of spellfiles.
        Provide null to keep it in XDG state folder.
      '';
    };
  };

  config =
    let
      nvim = cfg.package;
    in
    lib.mkIf cfg.enable {
      home.packages = [ nvim ];

      home.sessionVariables = {
        # should be like that but many programs don't respect VISUAL in favor of EDITOR so...
        # EDITOR = "nvim -u NONE -e";
        EDITOR = lib.mkDefault (lib.getExe nvim);
        VISUAL = lib.mkDefault (lib.getExe nvim);
        GIT_EDITOR = lib.mkDefault "${lib.getExe nvim} --clean";
        MANPAGER = lib.mkDefault "${lib.getExe nvim} --clean +Man!";
      };

      xdg.stateFile."nvim/notes" = {
        enable = cfg.notesPath != null;
        source = cfg.notesPath;
      };

      xdg.stateFile."nvim/spell" = {
        enable = cfg.spellPath != null;
        source = cfg.spellPath;
      };

      programs.git.ignores = lib.mkIf cfg.extendGitIgnores [
        ".netcoredbg_hist"
        ".nvim.lua"
      ];

      programs.bash.shellAliases = lib.mkDefault { vimdiff = "nvim -d"; };
      programs.fish.shellAliases = lib.mkDefault { vimdiff = "nvim -d"; };
      programs.zsh.shellAliases = lib.mkDefault { vimdiff = "nvim -d"; };
    };
}
